#version 460 core

layout (local_size_x = 32, local_size_y = 16) in;

#define LIGHTMAP_DIVISOR 2
#define PI 3.1415926535897932384626433832795
#define MAX_LIGHTS_COUNT 32

struct Light
{
    vec4 locationAndAngle;
    vec4 rangeAndStartColor;
};

layout(std140) uniform ubo_lights
{
    Light lights[MAX_LIGHTS_COUNT];
};

layout(r8, binding = 0) uniform image2D occlusionImage;
layout(rgba8, binding = 1) uniform image2D outputImage;

void main()
{
    vec3 resultColor = vec3(0.0);
    const uvec2 pos = gl_GlobalInvocationID.xy;

    for(int idx = 0; idx < MAX_LIGHTS_COUNT; ++idx)
    {
        const float range = lights[idx].rangeAndStartColor.x;
        const vec2 lightPosition = lights[idx].locationAndAngle.xy;
        const float dist = length(lightPosition - pos);

        const float rawAngle = -atan(lightPosition.x - pos.x, lightPosition.y - pos.y);
        const float angle = mod(rawAngle + PI * 2, PI * 2) / (PI * 2);
        vec2 angleMinMax = lights[idx].locationAndAngle.zw;

        if(angleMinMax.x > angleMinMax.y)
            angleMinMax = vec2(angleMinMax.x, 1.0 + angleMinMax.y);

        // early abort, the next part is expensive
        if(dist > range || !((angle >= angleMinMax.x && angle <= angleMinMax.y) || (angle + 1.0 >= angleMinMax.x && angle + 1.0 <= angleMinMax.y)))
            continue;

        const vec3 startColor = lights[idx].rangeAndStartColor.yzw;

        // walk between pos and lightPosition to find occlusions
        vec2 tempPos = pos;
        int lineSteps = int(ceil(abs(lightPosition.x - pos.x) > abs(lightPosition.y - pos.y) ? abs(lightPosition.x - pos.x) : abs(lightPosition.y - pos.y)) / LIGHTMAP_DIVISOR);
        const vec2 lineInc = (lightPosition - pos) / lineSteps;

        float lightStrength = 1.0;
        while(lineSteps --> 0)
        {
            const vec2 nextPos = tempPos + lineInc;
            lightStrength *= 1.0 - imageLoad(occlusionImage, ivec2(tempPos / LIGHTMAP_DIVISOR)).x;

            tempPos = nextPos;
        }

        const float strength = max(0, range - dist) / range ;
        resultColor += startColor * strength * strength * lightStrength;
    }

    imageStore(outputImage, ivec2(pos), vec4(resultColor, 1.0));
}